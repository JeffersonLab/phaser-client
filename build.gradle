plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'com.diffplug.spotless' version '7.2.1'
}

description = "RF Phaser Client"
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.releaseDate = new Date().format('MMM dd yyyy')

tasks.withType(JavaCompile).configureEach {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
tasks.withType(Test).configureEach {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.glassfish:javax.json:1.1.4',
                    'io.netty:netty-all:4.2.4.Final',
                    'org.jlab:jlog:5.1.0',
                    'org.apache.poi:poi:5.4.1',
                    'org.apache.poi:poi-ooxml:5.4.1',
                    'com.oracle.database.jdbc:ojdbc11:23.9.0.25.07'

    testImplementation 'junit:junit:4.13.2'
}
application {
    mainClass.set('org.jlab.phaser.PhaserSwingClient')
    applicationDefaultJvmArgs = ['-Dsun.java2d.opengl=True', '-Dsun.java2d.d3d=true']
}
startScripts {
    classpath += files('config')

    // https://discuss.gradle.org/t/classpath-in-application-plugin-is-building-always-relative-to-app-home-lib-directory/2012
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\config', '%APP_HOME%\\config')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/config', '$APP_HOME/config')
    }
}
run {
    classpath += files('config')
}
distributions {
    main {
        contents {
            into('config') {
                from 'config'
            }
        }
    }
}
processResources {
    filesMatching('release.properties') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}
javadoc {
    // Netbeans Matisse generated Swing code missing comments
    options.addStringOption('Xdoclint:none', '-quiet')
    options.overview = "src/main/java/overview.html"
    options.source = 11
    options.with {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/', 'https://netty.io/4.2/api/'
    }
}
tasks.register('runClient', JavaExec) {
    dependsOn build
    classpath = files('src/test/resources')
    classpath += sourceSets.main.runtimeClasspath
    classpath += files('config')
    mainClass.set('org.jlab.phaser.PhaserSwingClient')
    systemProperties = [
            'java.util.logging.config.file': 'config/logging.properties',
            "sun.java2d.opengl": "True",
            "sun.java2d.d3d": "true"

    ]
}
tasks.register('runServer', JavaExec) {
    dependsOn build
    classpath = files('src/test/resources')
    classpath += sourceSets.test.runtimeClasspath;
    classpath += files('config')
    mainClass.set('org.jlab.phaser.TestServer')
    systemProperties = [
            'java.util.logging.config.file': 'config/logging.properties'
    ]
}
spotless {
    java {
        targetExclude("**/generated/**/*.java")
        googleJavaFormat()
    }
}